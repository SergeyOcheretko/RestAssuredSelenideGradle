plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    // JUnit Jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    // Test tools
    testImplementation 'com.codeborne:selenide:6.19.1'
    testImplementation 'io.rest-assured:rest-assured:5.3.2'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    testImplementation 'org.slf4j:slf4j-simple:2.0.9'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.9.2'
    testImplementation 'org.junit.platform:junit-platform-suite:1.10.0'
    // Allure
    testImplementation 'io.qameta.allure:allure-junit5:2.25.0'
}

allure {
    version = '2.25.0'
    useJUnit5 {
        version = '2.25.0'
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events "passed", "failed", "skipped"
        exceptionFormat "short"
    }
    systemProperty "selenide.logging", "false"
    outputs.upToDateWhen { false }
    // Поддержка запуска по тегам через -DincludeTags и -DexcludeTags
    doFirst {
        def includeProp = System.getProperty("includeTags")
        if (includeProp) {
            useJUnitPlatform {
                includeTags(*(includeProp.split(",") as String[]))
            }
        }
        def excludeProp = System.getProperty("excludeTags")
        if (excludeProp) {
            useJUnitPlatform {
                excludeTags(*(excludeProp.split(",") as String[]))
            }
        }
    }
}

// Кастомные задачи для разных прогонов по тегам
tasks.register("smokeTest", Test) {
    description = "Run only smoke-tagged tests"
    group = "verification"
    useJUnitPlatform {
        includeTags "smoke"
    }
}

tasks.register("regressionTest", Test) {
    description = "Run only regression-tagged tests"
    group = "verification"
    useJUnitPlatform {
        includeTags "regression"
    }
}

tasks.register("apiTest", Test) {
    description = "Run only api-tagged tests"
    group = "verification"
    useJUnitPlatform {
        includeTags "api"
    }
}